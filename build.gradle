defaultTasks 'build'

// Create a special Java-only group of included builds
def javaOnly = gradle.includedBuilds.findAll {
  it.name != 'deck' && it.name != 'deck-kayenta' && it.name != 'spin'
}

// We need top-level meta tasks, as Gradle does not automatically recurse through included builds to find tasks
// By default, these only interact with Java projects - the rest operate somewhat differently
tasks.register('assemble') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':assemble')
  }
}

tasks.register('build') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':build')
  }
}

tasks.register('check') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':check')
  }
}

tasks.register('clean') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':clean')
  }
}

tasks.register('publish') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':publish')
  }
}

tasks.register('test') { task ->
  javaOnly.each { build ->
    task.dependsOn build.task(':test')
  }
}

// Additional aliases including UI projects
tasks.register('buildAll') { task ->
  gradle.includedBuilds.each { build ->
    task.dependsOn build.task(':build')
  }
}

tasks.register('checkAll') { task ->
  gradle.includedBuilds.each { build ->
    task.dependsOn build.task(':check')
  }
}

tasks.register('cleanAll') { task ->
  gradle.includedBuilds.each { build ->
    task.dependsOn build.task(':clean')
  }
}

tasks.register('testAll') { task ->
  gradle.includedBuilds.each { build ->
    task.dependsOn build.task(':test')
  }
}

// Create aliases to start each service
tasks.register('clouddriver') {
  it.dependsOn gradle.includedBuild('clouddriver').task(':clouddriver-web:run')
}

tasks.register('deck-build') {
  it.dependsOn gradle.includedBuild('deck').task(':build')
}

tasks.register('deck') {
  it.dependsOn gradle.includedBuild('deck').task(':run')
}

tasks.register('echo') {
  it.dependsOn gradle.includedBuild('echo').task(':echo-web:run')
}

tasks.register('fiat') {
  it.dependsOn gradle.includedBuild('fiat').task(':fiat-web:run')
}

tasks.register('front50') {
  it.dependsOn gradle.includedBuild('front50').task(':front50-web:run')
}

tasks.register('gate') {
  it.dependsOn gradle.includedBuild('gate').task(':gate-web:run')
}

tasks.register('igor') {
  it.dependsOn gradle.includedBuild('igor').task(':igor-web:run')
}

tasks.register('kayenta') {
  it.dependsOn gradle.includedBuild('kayenta').task(':kayenta-web:run')
}

tasks.register('keel') {
  it.dependsOn gradle.includedBuild('keel').task(':keel-web:run')
}

tasks.register('orca') {
  it.dependsOn gradle.includedBuild('orca').task(':orca-web:run')
}

tasks.register('rosco') {
  it.dependsOn gradle.includedBuild('rosco').task(':rosco-web:run')
}
