/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'io.spinnaker.project' apply false
  alias(libs.plugins.kotlin.jvm) apply false
  alias(libs.plugins.kotlin.allopen) apply false
}

allprojects {
  apply plugin: 'io.spinnaker.project'
}

subprojects {
  group = "io.spinnaker.fiat"

  if (name != "fiat-bom") {
    apply plugin: 'java-library'
    apply plugin: 'groovy'

    test {
      testLogging {
        exceptionFormat = 'full'
      }
      minHeapSize = "512m"
      maxHeapSize = "512m"
      useJUnitPlatform()
    }
    tasks.withType(Javadoc) {
      failOnError = false
    }

    tasks.withType(JavaExec) {
      if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7103'
      }
    }

    dependencies {
      implementation enforcedPlatform("io.spinnaker.kork:kork-bom")
      compileOnly "org.projectlombok:lombok"
      annotationProcessor platform("io.spinnaker.kork:kork-bom")
      annotationProcessor "org.projectlombok:lombok"
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
      testAnnotationProcessor platform("io.spinnaker.kork:kork-bom")
      testAnnotationProcessor "org.projectlombok:lombok"

      testImplementation("org.springframework.boot:spring-boot-starter-test") {
         exclude group: "org.slf4j", module: "slf4j-api"
      }
      testImplementation "org.spockframework:spock-core"
      testImplementation "org.spockframework:spock-spring"
      testImplementation "org.springframework:spring-test"
      testImplementation "org.hamcrest:hamcrest-core"
      testRuntimeOnly "cglib:cglib-nodep"
      testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
      testRuntimeOnly "org.objenesis:objenesis"
    }
  }
}
