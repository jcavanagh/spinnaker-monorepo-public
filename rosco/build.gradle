plugins {
  id 'io.spinnaker.project' apply false
}

allprojects {
  apply plugin: 'io.spinnaker.project'
}

subprojects {
  group = "io.spinnaker.rosco"

  if (name != "rosco-bom") {
    apply plugin: 'java-library'
    apply plugin: "groovy"

    dependencies {
      implementation enforcedPlatform("io.spinnaker.kork:kork-bom")

      compileOnly "org.projectlombok:lombok"

      annotationProcessor enforcedPlatform("io.spinnaker.kork:kork-bom")
      annotationProcessor "org.projectlombok:lombok"
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

      testCompileOnly "org.projectlombok:lombok"

      testAnnotationProcessor enforcedPlatform("io.spinnaker.kork:kork-bom")
      testAnnotationProcessor "org.projectlombok:lombok"

      testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    test {
      testLogging {
        exceptionFormat = 'full'
      }
      useJUnitPlatform()
    }
  }

  tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
      jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8187'
    }
  }

}
